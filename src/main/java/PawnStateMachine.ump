namespace ca.mcgill.ecse223.quoridor.controller;

external List {}
external ArrayList {}

use ca.mcgill.ecse223.quoridor.model.MoveDirection;
use QuoridorGame.ump;


class PawnBehaviour{
	depend java.util.List;
	depend java.util.ArrayList;

	0..1 -> 0..1 Game currentGame;
	0..1 -> 0..1 Player player;


  pawnSM{

    positionStates{

        RowState{

            initialVerticalState{
                entry [ playerIsBlack() ] -> OnNorthBorder;
                entry [ playerIsWhite() ] -> OnSouthBorder;
            }

            OnNorthBorder{
                move(MoveDirection dir) [dir == MoveDirection.South && isLegalStep(dir)] -> NearNorthBorder;
                jump(MoveDirection dir) [(dir == MoveDirection.SouthEast || dir == MoveDirection.SouthWest) && isLegalJump(dir)] -> NearNorthBorder;
                jump(MoveDirection dir) [dir == MoveDirection.South && isLegalJump(dir)] -> BetweenNorthSouthBorders;
            }
            NearNorthBorder{
                move(MoveDirection dir) [dir == MoveDirection.North && isLegalStep(dir)] -> OnNorthBorder;
                move(MoveDirection dir) [dir == MoveDirection.South && isLegalStep(dir)] -> BetweenNorthSouthBorders;
                jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.NorthWest) && isLegalJump(dir)] -> OnNorthBorder;
                jump(MoveDirection dir) [(dir == MoveDirection.SouthEast || dir == MoveDirection.SouthWest) && isLegalJump(dir)] -> BetweenNorthSouthBorders;
                jump(MoveDirection dir) [dir == MoveDirection.South && isLegalJump(dir)] -> BetweenNorthSouthBorders;
            }
            BetweenNorthSouthBorders{
                move(MoveDirection dir) [dir == MoveDirection.North && getCurrentPawnRow() == 3 && isLegalStep(dir)] -> NearNorthBorder;
                move(MoveDirection dir) [dir == MoveDirection.North && getCurrentPawnRow() >= 4 && isLegalStep(dir)] -> BetweenNorthSouthBorders;	//Self Transition
                jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.NorthWest) && getCurrentPawnRow() == 3 && isLegalJump(dir)] -> NearNorthBorder;
                jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.NorthWest) && getCurrentPawnRow() >= 4 && isLegalJump(dir)] -> BetweenNorthSouthBorders;	//Self Transition
                jump(MoveDirection dir) [dir == MoveDirection.North && getCurrentPawnRow() == 3 && isLegalJump(dir)] -> OnNorthBorder;
                jump(MoveDirection dir) [dir == MoveDirection.North && getCurrentPawnRow() == 4 && isLegalJump(dir)] -> NearNorthBorder;
                jump(MoveDirection dir) [dir == MoveDirection.North && getCurrentPawnRow() >= 5 && isLegalJump(dir)] -> BetweenNorthSouthBorders;	//Self Transition
                move(MoveDirection dir) [dir == MoveDirection.South && getCurrentPawnRow() == 7 && isLegalStep(dir)] -> NearSouthBorder;
                move(MoveDirection dir) [dir == MoveDirection.South && getCurrentPawnRow() <= 6 && isLegalStep(dir)] -> BetweenNorthSouthBorders;	//Self Transition
                jump(MoveDirection dir) [(dir == MoveDirection.SouthEast || dir == MoveDirection.SouthWest) && getCurrentPawnRow() == 7 && isLegalJump(dir)] -> NearSouthBorder;
                jump(MoveDirection dir) [(dir == MoveDirection.SouthEast || dir == MoveDirection.SouthWest) && getCurrentPawnRow() <= 6 && isLegalJump(dir)] -> BetweenNorthSouthBorders;	//Self Transition
                jump(MoveDirection dir) [dir == MoveDirection.South && getCurrentPawnRow() == 7 && isLegalJump(dir)] -> OnSouthBorder;
                jump(MoveDirection dir) [dir == MoveDirection.South && getCurrentPawnRow() == 6 && isLegalJump(dir)] -> NearSouthBorder;
                jump(MoveDirection dir) [dir == MoveDirection.South && getCurrentPawnRow() <= 5 && isLegalJump(dir)] -> BetweenNorthSouthBorders;	//Self Transition
            }
            NearSouthBorder{
                move(MoveDirection dir) [dir == MoveDirection.South && isLegalStep(dir)] -> OnSouthBorder;
                move(MoveDirection dir) [dir == MoveDirection.North && isLegalStep(dir)] -> BetweenNorthSouthBorders;
                jump(MoveDirection dir) [(dir == MoveDirection.SouthEast || dir == MoveDirection.SouthWest) && isLegalJump(dir)] -> OnSouthBorder;
                jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.NorthWest) && isLegalJump(dir)] -> BetweenNorthSouthBorders;
                jump(MoveDirection dir) [dir == MoveDirection.North && isLegalJump(dir)] -> BetweenNorthSouthBorders;
            }
            OnSouthBorder{
                move(MoveDirection dir) [dir == MoveDirection.North && isLegalStep(dir)] -> NearSouthBorder;
                jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.NorthWest) && isLegalJump(dir)] -> NearSouthBorder;
                jump(MoveDirection dir) [dir == MoveDirection.North && isLegalJump(dir)] -> BetweenNorthSouthBorders;
            }
        }
        ||
        ColumnState{

            BetweenWestEastBorders{
	            move(MoveDirection dir) [dir == MoveDirection.West && getCurrentPawnColumn() == 3 && isLegalStep(dir)] -> NearWestBorder;
	            move(MoveDirection dir) [dir == MoveDirection.West && getCurrentPawnColumn() >= 4 && isLegalStep(dir)] -> BetweenWestEastBorders;	//Self Transition
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthWest || dir == MoveDirection.SouthWest) && getCurrentPawnColumn() == 3 && isLegalJump(dir)] -> NearWestBorder;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthWest || dir == MoveDirection.SouthWest) && getCurrentPawnColumn() >= 4 && isLegalJump(dir)] -> BetweenWestEastBorders;	//Self Transition
	            jump(MoveDirection dir) [dir == MoveDirection.West && getCurrentPawnColumn() == 3 && isLegalJump(dir)] -> OnWestBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.West && getCurrentPawnColumn() == 4 && isLegalJump(dir)] -> NearWestBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.West && getCurrentPawnColumn() >= 5 && isLegalJump(dir)] -> BetweenWestEastBorders;	//Self Transition
	            move(MoveDirection dir) [dir == MoveDirection.East && getCurrentPawnColumn() == 7 && isLegalStep(dir)] -> NearEastBorder;
	            move(MoveDirection dir) [dir == MoveDirection.East && getCurrentPawnColumn() <= 6 && isLegalStep(dir)] -> BetweenWestEastBorders;	//Self Transition
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.SouthEast) && getCurrentPawnColumn() == 7 && isLegalJump(dir)] -> NearEastBorder;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.SouthEast) && getCurrentPawnColumn() <= 6 && isLegalJump(dir)] -> BetweenWestEastBorders;	//Self Transition
	            jump(MoveDirection dir) [dir == MoveDirection.East && getCurrentPawnColumn() == 7 && isLegalJump(dir)] -> OnEastBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.East && getCurrentPawnColumn() == 6 && isLegalJump(dir)] -> NearEastBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.East && getCurrentPawnColumn() <= 5 && isLegalJump(dir)] -> BetweenWestEastBorders;	//Self Transition
            }

            OnWestBorder{
	            move(MoveDirection dir) [dir == MoveDirection.East && isLegalStep(dir)] -> NearWestBorder;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.SouthEast) && isLegalJump(dir)] -> NearWestBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.East && isLegalJump(dir)] -> BetweenWestEastBorders;
            }
            NearWestBorder{
	            move(MoveDirection dir) [dir == MoveDirection.West && isLegalStep(dir)] -> OnWestBorder;
	            move(MoveDirection dir) [dir == MoveDirection.East && isLegalStep(dir)] -> BetweenWestEastBorders;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthWest || dir == MoveDirection.SouthWest) && isLegalJump(dir)] -> OnWestBorder;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.SouthEast) && isLegalJump(dir)] -> BetweenWestEastBorders;
	            jump(MoveDirection dir) [dir == MoveDirection.East && isLegalJump(dir)] -> BetweenWestEastBorders;
            }
            NearEastBorder{
	            move(MoveDirection dir) [dir == MoveDirection.West && isLegalStep(dir)] -> BetweenWestEastBorders;
	            move(MoveDirection dir) [dir == MoveDirection.East && isLegalStep(dir)] -> OnEastBorder;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthWest || dir == MoveDirection.SouthWest) && isLegalJump(dir)] -> BetweenWestEastBorders;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthEast || dir == MoveDirection.SouthEast) && isLegalJump(dir)] -> OnEastBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.West && isLegalJump(dir)] -> BetweenWestEastBorders;
            }
            OnEastBorder{
	            move(MoveDirection dir) [dir == MoveDirection.West && isLegalStep(dir)] -> NearEastBorder;
	            jump(MoveDirection dir) [(dir == MoveDirection.NorthWest || dir == MoveDirection.SouthWest) && isLegalJump(dir)] -> NearEastBorder;
	            jump(MoveDirection dir) [dir == MoveDirection.West && isLegalJump(dir)] -> BetweenWestEastBorders;
            }
        }

    }

  }


	// Returns the current row number of the pawn
	int getCurrentPawnRow() {
		PlayerPosition currentPosition = getCurrentPlayerPosition();
		return currentPosition.getTile().getRow();
	}
	// Returns the current column number of the pawn
	int getCurrentPawnColumn() {
		PlayerPosition currentPosition = getCurrentPlayerPosition();
		return currentPosition.getTile().getColumn();
	}
	// Returns if it is legal to step in the given direction
	boolean isLegalStep(MoveDirection dir) {
	    boolean stepWasLegal = true;

		//First, sanity check that no diagonal moves are being asked. Diagonal moves are exclusive for jumps.
		if( dir == MoveDirection.NorthEast || dir == MoveDirection.NorthWest || dir == MoveDirection.SouthEast || dir == MoveDirection.SouthWest ){
			illegalMove();
			return false;
		}

	    //Compute useful data
		int initialRow = getCurrentPawnRow();
		int initialCol = getCurrentPawnColumn();
		int finalRow = initialRow;
		int finalCol = initialCol;
		int otherRow = getOtherPawnRow();       //Row of other pawn
		int otherCol = getOtherPawnColumn();    //Column of other pawn
		//Check if the final destination is out of bounds
		if(!moveIsInBounds( initialRow, initialCol, dir )){
		    stepWasLegal =  false;
		}
		//Check if the path is blocked by a wall
        if( pathIsBlockedByWall( initialRow, initialCol, dir ) ){
            stepWasLegal =  false;   //If the move is blocked by a wall
        }
		//Get the final coordinates
		switch( dir ) {
			case East:
				//Compute new position
				finalRow = initialRow;
				finalCol = initialCol + 1;
				break;
			case West:
				//Compute new position
				finalRow = initialRow;
				finalCol = initialCol - 1;
				break;
			case North:
				//Compute new position
				finalRow = initialRow - 1;
				finalCol = initialCol;
				break;
			case South:
				//Compute new position
				finalRow = initialRow + 1;
				finalCol = initialCol;
				break;
		}
		//Check if pawns would conflict with move
		if( finalCol == otherCol && finalRow == otherRow ){
			stepWasLegal =  false;
		}

		//If any test failed, call the illegalMove method
		if( !stepWasLegal ){
		    illegalMove();
		}

		//All tests passed.
		return stepWasLegal;
	}
	// Returns if it is legal to jump in the given direction
	//	This is essentially a wrapper method for the original isLegalJump method which has been renamed to isLegalJumpFromAdjacentPawn, which only works with simple North, East, South, and West MoveDirection inputs.
	boolean isLegalJump(MoveDirection dir) {
		boolean caughtIllegalSymptoms = false;
		
		//Compute useful data for calculating
		int initialRow = getCurrentPawnRow();       //Starting row of player's pawn
		int initialCol = getCurrentPawnColumn();    //Starting column of player's pawn
		int otherRow = getOtherPawnRow();           //Current row of other player's pawn
		int otherCol = getOtherPawnColumn();        //Current column of other player's pawn
		int deltaRow = otherRow - initialRow;       //Displacement between other player's pawn and player's pawn in row.
		int deltaCol = otherCol - initialCol;       //Displacement between other player's pawn and player's pawn in column.
		//First check if the enemy pawn is adjacent. If not, return false.
		if( initialRow != otherRow && initialCol != otherCol ){
			caughtIllegalSymptoms =  true;
		} else if ( deltaRow == 0 ) {
			if( deltaCol != 1 && deltaCol != -1 ){
				caughtIllegalSymptoms =  true;   //If you're on the same row but too far away from another still.
			}
		} else if ( deltaCol == 0 ) {
			if( deltaRow != 1 && deltaRow != -1 ){
				caughtIllegalSymptoms =  true;   //If you're on the same column but too far away from another still.
			}
		}
		if(caughtIllegalSymptoms){
			illegalMove();
			return false;
		}
		//Now figure out what direction the adjacent pawn is to our current pawn, and verify it does not counter our dir.
		MoveDirection adjacentPawnDir;
		if( deltaCol == 1 ){
			adjacentPawnDir = MoveDirection.East;
		} else if ( deltaRow == 1 ) {
			adjacentPawnDir = MoveDirection.South;
		} else if ( deltaCol == -1 ) {
			adjacentPawnDir = MoveDirection.West;
		} else if ( deltaRow == -1 ) {
			adjacentPawnDir = MoveDirection.North;
		} else {
			throw new IllegalArgumentException("Detected unacceptable MoveDirection.");
		}
		//Now that we know what direction our adjacent pawn is, let us derive what is the relative direction from that adjacent pawn that our directional input is.
		MoveDirection relativeJumpDirectionToAdjacentPawn = null;
		if( adjacentPawnDir == MoveDirection.North ){
			if( dir == MoveDirection.North ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.North;
			} else if( dir == MoveDirection.NorthEast ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.East;
			} else if( dir == MoveDirection.NorthWest ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.West;			
			} else {
				caughtIllegalSymptoms = true;	//If your vector doesn't head toward the enemy pawn, you can't jump off them.
			}
			
		} else if( adjacentPawnDir == MoveDirection.East ){
			if( dir == MoveDirection.East ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.East;
			} else if( dir == MoveDirection.NorthEast ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.North;
			} else if( dir == MoveDirection.SouthEast ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.South;
			} else {
				caughtIllegalSymptoms = true;	//If your vector doesn't head toward the enemy pawn, you can't jump off them.
			}
			
		} else if( adjacentPawnDir == MoveDirection.South ){
			if( dir == MoveDirection.South ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.South;
			} else if( dir == MoveDirection.SouthEast ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.East;
			} else if( dir == MoveDirection.SouthWest ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.West;
			} else {
				caughtIllegalSymptoms = true;	//If your vector doesn't head toward the enemy pawn, you can't jump off them.
			}
			
			
		} else {	//Insinuated that in this case that adjacentPawnDir == MoveDirection.West.
			if( dir == MoveDirection.West ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.West;
			} else if( dir == MoveDirection.NorthWest ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.North;
			} else if( dir == MoveDirection.SouthWest ){
				relativeJumpDirectionToAdjacentPawn = MoveDirection.South;
			} else {
				caughtIllegalSymptoms = true;	//If your vector doesn't head toward the enemy pawn, you can't jump off them.
			}
			
		}
		if(caughtIllegalSymptoms){
			illegalMove();
			return false;
		}
		//Now that we know which direction relative of the adjacent pawn we wish to jump towards, we can run the full tests for that.
		return isLegalJumpFromAdjacentPawn( adjacentPawnDir , relativeJumpDirectionToAdjacentPawn );
		
		
	}
	// Helper Method: Returns if it is legal to jump the tile in the given direction of the adjacent pawn.
	private boolean isLegalJumpFromAdjacentPawn(MoveDirection adjacentPawnDir, MoveDirection dir) {
	    boolean stepWasLegal = true;

		//Compute useful data for calculating
		int initialRow = getCurrentPawnRow();       //Starting row of player's pawn
		int initialCol = getCurrentPawnColumn();    //Starting column of player's pawn
		int otherRow = getOtherPawnRow();           //Current row of other player's pawn
		int otherCol = getOtherPawnColumn();        //Current column of other player's pawn
		//Now that we know a pawn is adjacent, we need to figure out if there is a wall in between.
		if( pathIsBlockedByWall( initialRow, initialCol, adjacentPawnDir ) ){
			stepWasLegal =  false;   //If the move is prevented by a wall blocking access to the adjacent pawn.
		}
		//Now that we know the first step of the jump is clear of walls, let us now see if moving in the provided direction from the adjacent pawn is blocked by a wall.
		if( pathIsBlockedByWall( otherRow, otherCol, dir ) ){
			stepWasLegal =  false;   //The jump is blocked from the adjacent pawn's position.
		}
		//The check before checks to see if there is a wall in the way to jumping over the opponent, but does not consider that a wall needs to be behind the opponent for jumping to the opp's side.
		//Now we check, if the jump is a valid direction and not to the opposite side, if there was that requisite board.
		if( dir != adjacentPawnDir ){	//If we are jumping to the side of the opponent, not behind
			if( !pathIsBlockedByWall( otherRow, otherCol, adjacentPawnDir ) ) {	//If there is no wall behind the opponent
				stepWasLegal = false;
			}
		}
		//Let's not forget that we need to check that the final destination is on the board.
		if(!moveIsInBounds( otherRow, otherCol, dir )){
		    stepWasLegal =  false;
		}

		//If any of the tests failed, call the illegalMove alerter.
		if( !stepWasLegal ){
		    illegalMove();
		}
		//Return whether or not the step was legal.
		return stepWasLegal;
	}

	// Action to be called when an illegal move is attempted
	void illegalMove() {
		throw new IllegalArgumentException("Detected an illegal move.");
	}

    /*
     *  THIS ENUMERATION, UNSUPPORTED BY UMPLE, HAS BEEN MANUALLY ADDED INTO THE MODELS FOLDER.
     */
	// Enumeration for the possible moving directions
	// (directions are from the viewpoint of white player)
	//enum MoveDirection { East, South, West, North; }


	//Helper: Gets the current position of the player
	private PlayerPosition getCurrentPlayerPosition() {
		PlayerPosition playerPosition;
		if( player.getGameAsWhite() != null ){
			playerPosition = player.getGameAsWhite().getCurrentPosition().getWhitePosition();
		} else {
			playerPosition = player.getGameAsBlack().getCurrentPosition().getBlackPosition();
		}
		return playerPosition;
	}
	//Helper: Gets the current position of the other player
	private PlayerPosition getCurrentOtherPlayerPosition() {
		PlayerPosition playerPosition;
		if( player.getGameAsWhite() != null ){
			playerPosition = player.getGameAsWhite().getCurrentPosition().getBlackPosition();
		} else {
			playerPosition = player.getGameAsBlack().getCurrentPosition().getWhitePosition();
		}
		return playerPosition;
	}
	//Helper: Gets the current row number of the other player
	private int getOtherPawnRow(){
		return getCurrentOtherPlayerPosition().getTile().getRow();
	}
	//Helper: Gets the current column number of the other player
	private int getOtherPawnColumn(){
		return getCurrentOtherPlayerPosition().getTile().getColumn();
	}
	//Helper: Gets all of the walls placed onto the board
	private List<Wall> getAllWallsOnBoard() {
		List<Wall> placedWalls = new ArrayList<Wall>();
		placedWalls.addAll( currentGame.getCurrentPosition().getBlackWallsOnBoard() );
		placedWalls.addAll( currentGame.getCurrentPosition().getWhiteWallsOnBoard() );
		return placedWalls;
	}
	//Helper: Returns if a path off of a provided tile, through row and column, towards a direction is blocked by a wall
	private boolean pathIsBlockedByWall( int row, int col, MoveDirection dir ){
		for( Wall wall : getAllWallsOnBoard() ){
			//Check if the wall's orientation is capable of blocking the direction.
			if( wall.getMove().getWallDirection() == Direction.Horizontal ){
				if( dir == MoveDirection.East || dir == MoveDirection.West ){
					continue;   //Skip the horizontal wall which can't block horizontal movement.
				}
			} else {
				if( dir == MoveDirection.North || dir == MoveDirection.South ){
					continue;   //Skip the vertical wall which can't block vertical movement.
				}
			}
			//Compute useful numbers.
			int wallRow = wall.getMove().getTargetTile().getRow();
			int wallCol = wall.getMove().getTargetTile().getColumn();
			int deltaRow = wallRow - row;
			int deltaCol = wallCol - col;
			switch ( dir ) {
				case East:
					//Check if the path to the right of col row is blocked by wall. If so, then flag.
					if( deltaCol == 0 ){
						if( deltaRow == -1 || deltaRow == 0 ){
							return true;    //Return true for path is blocked by wall
						}
					}
					break;
				case West:
					//Check if the path to the left of col row is blocked by wall. If so, then flag.
					if( deltaCol == -1 ){
						if( deltaRow == -1 || deltaRow == 0 ){
							return true;    //Return true for path is blocked by wall
						}
					}
					break;
				case North:
					//Check if the path to the top of col row is blocked by wall. If so, then flag.
					if( deltaRow == -1 ){
						if( deltaCol == -1 || deltaCol == 0 ){
							return true;    //Return true for path is blocked by wall
						}
					}
					break;
				case South:
					//Check if the path to the bottom of col row is blocked by wall. If so, then flag.
					if( deltaRow == 0 ){
						if( deltaCol == -1 || deltaCol == 0 ){
							return true;    //Return true for path is blocked by wall
						}
					}
					break;
			}
		}
		//No walls were detected to be blocking its path; return false for path is blocked by wall.
		return false;
	}
	//Helper: Returns if a move in the dir off of the provided tile coordinates is valid
	private boolean moveIsInBounds( int initialRow, int initialCol, MoveDirection dir ){
		int finalRow;
		int finalCol;
		switch( dir ) {
			case East:
				//Check if the player is already on the east border
				if( initialCol == 9 ){
					return false;
				}
				break;
			case West:
				//Check if the player is already on the west border
				if( initialCol == 0 ){
					return false;
				}
				break;
			case North:
				//Check if the player is already on the north border
				if( initialRow == 0 ){
					return false;
				}
				break;
			case South:
				//Check if the player is already on the south border
				if( initialRow == 9 ){
					return false;
				}
				break;
		}
		//All tests passed.
		return true;
	}
    //Helper method for knowing if the player has a white pawn
    private boolean playerIsWhite(){
        return player.getGameAsWhite() != null;
    }
    //Helper method for knowing if the player has a black pawn
    private boolean playerIsBlack(){
        return player.getGameAsBlack() != null;
    }

  }
